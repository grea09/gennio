---
title: "2. Color"
author: Antoine Gréa
bibliography: bibliography/thesis.bib
style: thesis
---

# General Planning Framework

When making artificial inteligence systems, an important feature is the ability to make decisions and act accordingly. To act, one should plan ahead. This is why the field of automated planning is being actively researched in order to find efficient algorithms to find the best course of action in any given situation. The previous chapter allowed to lay the bases of knowledge representation. How knowledge about eh planning domains are represented is a main factor to take into account in order to choose any planning algorithm.

Automated planning really started being formally investigated after the creation of the Stanford Research Institute Problem Solver (STRIPS) by @fikes_strips_1971. This is one of the most influencial planner, not because of its algorithm but because of its input language. Any planning system needs a way to express the information related to the input problem. Any language done for this purpose is called an *action language*. STRIPS will be mostly remembered for its eponymous action language that is at the base of any modern derivatives.

Other languages were introduced to express planning domains like ADL [@pednault_adl_1989]. That formalism adds negation and conjunction into literals to STRIPS. It also drops the closed world hypothesis for an open world one: anything not stated in conditions (initial or action effects) is unknown.

The current standard was strongly inspired by @penberthy_ucpop_1992 and his UCPOP planner. Like STRIPS, UCPOP had a planning domain language that was probably the most expressive of its time. It differs from ADL by merging the add and delete lists in effects and to change both preconditions and effects of actions into logic formula instead of simple states.

All action language is based on mainly two notions: *actions* and *states*. A state is a set of *fluents* that describe aspects of the world modelized by the domain. Each action has a logic formula over states that allows its correct execution. This requirement is called *precondition*. The mirror image of this notion are possible *effects* which are logic formula that are enforced on the current state after the action is executed. The domain is completed with a problem, most of the time specified in a separate file. The problem basically contains two states: the *initial* and *goal* states. These definitions will be properly formalized in @sec:framework.

## Existing Languages and Frameworks

### Classical

Nowadays, the language most recognised by the automated planning comunity is Planning Domain Definition Language (PDDL). It is strongly inspired by STRIPS and uses a Lisp-like syntax to express planning domains and problems.

This language was created for the first major automated planning competition hosted by AIPS in 1998 [@ghallab_pddl_1998]. It came along with a syntax and solution checker writen in LISP. It was introduced as a way to standardize notation of planning domains and problems so that libraries of standard problems can be used for benchmarks. The main goal of the language was to be able to express most of the planning problems of the time.

With time, the planning competitions became known under the name of International Planning Competitions (IPC) regularly hosted by the ICAPS conference. With each installment, the language evolved to address issues encountered the previous years. The current version of PDDL is 3.1 [@kovacs_bnf_2011]. Its syntax, goes similarly as described in @lst:pddl_syntax.

```{#lst:pddl_syntax .lisp caption="Simplified explanation of the syntax of PDDL." escapechar=$}
(define (domain <domain-name>)
  (:requirements :<requirement-name>)
  (:types <type-name>)
  (:constants <constant-name> - <constant-type>)
  (:predicates (<predicate-name> ?<var> - <var-type>))
  (:functions	(<function-name> ?<var> - <var-type>) - <function-type>)

  (:action <action-name>
	     :parameters (?<var> - <var-type>)
	     :precondition (and (= (<function-name> ?<var>) <value>) (<predicate-name> ?<var>))
	     :effect
	     (and (not (<predicate-name> ?<var>))
		   (assign (<function-name> ?<var>) ?<var>)))
```

PDDL uses the functional notation style of LISP. It defines usually two files: one for the domain and one for the problem instance. The domain describes constants, fluents and all possible actions. The problem lays the initial and goal states description.

For example, consider the classic block world domain expressed in @lst:block_pddl. It uses a predicate to express whether a block is on the table because several blocks can be on the table at once. However it uses a 0-ary function to describe the one block allowed to be held at a time. The description of the stack of blocks is done with an unary function to give the block that is on top of another one. To be able to express the absence of blocks it uses a constant named `no-block`. All the actions described are pretty straightforward: `stack` and `unstack` make sure it is possible to add or remove a block before doing it and `pick-up` and `put-down` manages the handling operations.

```{#lst:block_pddl .lisp caption="Classical PDDL 3.0 definition of the domain Block world" escapechar=$}
(define (domain BLOCKS-object-fluents)
  (:requirements :typing :equality :object-fluents)
  (:types block)
  (:constants no-block - block)
  (:predicates (on-table ?x - block))
  (:functions	(in-hand) - block
		(on-block ?x - block) - block) ;;what is in top of block ?x

  (:action pick-up
	     :parameters (?x - block)
	     :precondition (and (= (on-block ?x) no-block) (on-table ?x) (= (in-hand) no-block))
	     :effect
	     (and (not (on-table ?x))
		   (assign (in-hand) ?x)))

  (:action put-down
	     :parameters (?x - block)
	     :precondition (= (in-hand) ?x)
	     :effect
	     (and (assign (in-hand) no-block)
		   (on-table ?x)))

  (:action stack
	     :parameters (?x - block ?y - block)
	     :precondition (and (= (in-hand) ?x) (= (on-block ?y) no-block))
	     :effect
	     (and (assign (in-hand) no-block)
	   	  (assign (on-block ?y) ?x)))

  (:action unstack
	     :parameters (?x - block ?y - block)
	     :precondition (and (= (on-block ?y) ?x) (= (on-block ?x) no-block) (= (in-hand) no-block))
	     :effect
	     (and (assign (in-hand) ?x)
		  (assign (on-block ?y) no-block))))
```
However, PDDL is far from an universal standard. Some efforts have been made to try and standardize the domain of automated planning in the form of optional requirements. The latest of the PDDL standard is the version 3.1 [@kovacs_bnf_2011]. It has 18 atomic requirements as represented in @fig:pddl_req. Most requirements are parts of PDDL that either increase the complexity of planning significantly or that require extra implementation effort to meet.

![Dependancies and grouping of PDDL requirements.](graphics/pddl_requirements.svg){#fig:pddl_req}

Even with that flexibility, PDDL is unable to cover all of automated planning paradigms. This caused most subdomains of automated planning to be left in a state similar to before PDDL: a zoo of languages and derivatives that aren't interoperable. The reason for this is the fact that PDDL isn't expressive enough to encode more than a limited variation in action and fluent description.

Another problem is that PDDL isn't made to be used by planners to help with their planning process. Most planners will totally separate the compilation of PDDL before doing any planning, so much so that most planners of the latest IPC used a framework that translates PDDL into a useful form before planning, adding computation time to the planning process. The list of participating planners and their use of language is presented in @tbl:ipc.

Name          Trans Pre Lang     Base      Rank
----          ----- --- ----     ----      ----
Delfi         Yes   Yes C++      FD        1
Complementary Yes   Yes C++      FD        2
Planning-PDBs Yes   Yes C++      FD        3
Scorpion      Yes   Yes C++      FD        4
FDMS          Yes   Yes C++      FD        5
DecStar       Yes   Yes C++      LAMA      6
Metis         Yes   Yes C++      FD        7
MSP           Yes   Yes Lisp     FD        8
Symple        Yes   Yes C++      FD        9
Ma-plan       No    Yes C        None      10

: Planners participating in the Classic track of the 2018 International Planning Competition (IPC). The table states whether the planner used a translation and a preprocessing system to handle PDDL. Most of the planners are based on FastDownward directly. {#tbl:ipc}

The domain is so diverse that attempts to unify it haven't succeeded so far. The main reason behind this is that some paradigms are vastly different from the classical planning description. Sometimes just adding a seamingly small feature like probabilities or plan reuse can make for a totally different problem. In the next section we describe planning paradigms and how they differ from classical planning along with their associated languages.

### Temporality oriented

When planning, time can become a sensitve constraint. Some critical tasks may required to be completed within a certain time. Actions with durations are already a feature of PDDL 3.1. However, PDDL might not provide support for external events (i.e. events occuring independant from the agent). To do this one must use another language.

#### PDDL+

PDDL+ is an extension of PDDL 2.1 that handles process and events [@fox_pddl_2002]. It can be viewed as similar to PDDL 3.1 continious effects but it differs on the expressivity. A process can have an effect on fluents at any time. They can happen either from the agent's own doing or being purely environemental. It might be possible in certain cases to modelize this using the durative actions, continious effects and timed initial literals of PDDL 3.1.

In @lst:pddl_plus, we reproduce an example from @fox_pddl_2002. It shows the syntax of durative actions in PDDL+. The timed precondition are also available in PDDL 3.1, but the `increase` and `decrease` rate of fluents is an exclusive feature of PDDL+.

```{#lst:pddl_plus .lisp caption="Example of PDDL+ durative action from Fox's paper." escapechar=$}
(:durative-action downlink
    :parameters (?r - recorder ?g - groundStation)
    :duration (> ?duration 0)
    :condition (and (at start (inView ?g))
                    (over all (inView ?g))
                    (over all (> (data ?r) 0)))
    :effect (and (increase (downlinked)
                      (* #t (transmissionRate ?g)))
                 (decrease (data ?r)
                      (* #t (transmissionRate ?g)))))
```
The main issue with durative actions is that time becomes a continous ressource that may change the values of fluents. The search for a plan in that context has a higher complexity than regular planning.

### Probabilistic

Sometimes, acting can become unpredictable. An action can fail for many reasons, from logical errors down to physical constraints. This call for a way to plan using probabilities with the ability to recover from any predicted failures. PDDL doesn't support using probabilities. That is why all IPC's tracks dealing with it always used another language than PDDL.

#### PPDDL

PPDDL is such a language. It was used during the 4^th^ and 5^th IPC for its probabilistic track [@younes_ppddl_2004]. It allows for probabilistic effects as demonstrated in @lst:ppddl. The planner must take into account the probability when chosing an action. The plan must be the most likely to succeed. But even with the best plan, failure can occur. This is why probabilistic planning often gives policies instead of a plan. A policy dictates the best choice in any given state, failure or not. While this allows for much more resilient execution, computation of policies are exponentially harder than classical planning. Indeed the planner needs to take into account every outcome of every action in the plan and react accordingly.

```{#lst:ppddl .lisp caption="Example of PPDDL use of probabilistic effects from Younes' paper." escapechar=$}
(define (domain bomb-and-toilet)
    (:requirements :conditional-effects :probabilistic-effects)
    (:predicates (bomb-in-package ?pkg) (toilet-clogged)
                  (bomb-defused))
    (:action dunk-package
             :parameters (?pkg)
             :effect (and (when (bomb-in-package ?pkg)
                                (bomb-defused))
                          (probabilistic 0.05 (toilet-clogged)))))
```

#### RDDL

Another language used by the 7^th^ IPC's uncertainty track is RDDL [@sanner_relational_2010a]. This language has been choosen because of its ability to express problems that are hard to encode in PDDL or PPDDL. Indeed, RDDL is capable of expressing Partially Observable Markovian Decision Process (POMDP) and Dynamic Bayesian Networks (DBN) in planning domains. This along with complex probability laws allows for easy implementation of most probabilistic planning problems. Its syntax differs greatly from PDDL, and seems closer to scala or C++. 
An example is provided in @lst:rddl from @sanner_relational_2010a. In it, we can see that actions in RDDL doesn't need preconditions or effects. In that case the reward is the closest information to the classical goal and the action is simply a parameter that will influence the probability distribution of the events that conditioned the reward.

``` {#lst:rddl .cpp caption="Example of RDDL syntax by Sanner." escapechar=$}
////////////////////////////////////////////////////////////////////////
// A simple propositional 2-slice DBN (variables are not parameterized).
//
// Author: Scott Sanner (ssanner [at] gmail.com)
////////////////////////////////////////////////////////////////////////
domain prop_dbn {
  	
	requirements = { reward-deterministic };
      	
	pvariables { 
		p : { state-fluent,  bool, default = false };
		q : { state-fluent,  bool, default = false };
		r : { state-fluent,  bool, default = false }; 
		a : { action-fluent, bool, default = false }; 
	};
  
	cpfs {
		// Some standard Bernoulli conditional probability tables
		p´ = if (p ^ r) then Bernoulli(.9) else Bernoulli(.3);
						
		q´ = if (q ^ r) then Bernoulli(.9) 
						else if (a) then Bernoulli(.3) else Bernoulli(.8);

		// KronDelta is like a DiracDelta, but for discrete data (boolean or int)
		r´ = if (~q) then KronDelta(r) else KronDelta(r <=> q);										
	};

	// A boolean functions as a 0/1 integer when a numerical value is needed	
	reward = p + q - r; // a boolean functions as a 0/1 integer when a numerical value is needed
}
        
instance inst_dbn {

	domain = prop_dbn;	
	init-state { 
		p = true;  // could also just say 'p' by itself 
		q = false; // default so unnecessary, could also say '~q' by itself
		r;         // same as r = true
	};
  
	max-nondef-actions = 1;
	horizon  = 20;
	discount = 0.9;
}
```

### Multi-agent

Planning can also be a collective effort. In some cases, a system must account for other agents trying to either cooperate or compete in achieving similar goals. The problem that arise is coordination. How to make a plan meant to be executed with several agents concurently ? Several multi-agent action languages have been proposed to answer that question.

#### MAPL

Another extension of PDDL 2.1, MAPL was introduced to handle synchronization of actions [@brenner_multiagent_2003]. This is done using modal operators over fluents. In that regard, MAPL is closer to the PDDL+ extension proposed earlier. It introduce durative actions that will later be integrated into the PDDL 3.0 standard. MAPL also introduce a synchronization mechanism using speech as a comunication vector. This seems very specific as explicit comunication isn't a requirement of collaborative work. @Lst:mapl is an example of the syntax of MAPL domains. PDDL 3.0 seems to share a similar syntax.

``` {#lst:mapl .lisp caption="Example of MAPL syntax by Brenner." escapechar=$}
(:state-variables
  (pos ?a - agent) - location
  (connection ?p1 ?p2 - place) - road
  (clear ?r - road) - boolean)
(:durative-action Move
  :parameters (?a - agent ?dst - place)
  :duration (:= ?duration (interval 2 4))
  :condition
    (at start (clear (connection (pos ?a) ?dst)))
  :effect (and
    (at start (:= (pos ?a) (connection (pos ?a) ?dst)))
    (at end (:= (pos ?a) ?dst))))
```

#### MA-PDDL

Another aspect of multi-agent planning is the ability to affect tasks and to manage interactions between agents efficiently. For this MA-PDDL seems more adapted than MAPL. It is an extension of PDDL 3.1, that makes easier to plan for a team of heteroneous agents [@kovacs_multiagent_2012]. In the example in @lst:ma-pddl, we can see how action can be affected to agents. While it makes the representation easier, it is possible to obtain similar effect by passing an agent object as parameter of an action in PDDL 3.1. More complex expressions are possible in MA-PDDL, like referencing the action of other agents in the preconditions of actions or the ability to affect different goals to different agents. Later on, MA-PDDL was extended with probabilistic capabilities inspired by PPDDL [@kovacs_converting_2013].

``` {#lst:ma-pddl .lisp caption="Example of MA-PDDL syntax by Kovacs." escapechar=$}
(define (domain ma-lift-table)
(:requirements :equality :negative-preconditions 
               :existential-preconditions :typing :multi-agent) 
(:types agent) (:constants table)
(:predicates (lifted (?x - object) (at ?a - agent ?o - object))
(:action lift :agent ?a - agent :parameters ()
:precondition (and (not (lifted table)) (at ?a table) 
              (exists (?b - agent)
               (and (not (= ?a ?b)) (at ?b table) (lift ?b)))) 
:effect (lifted table)))
```

### Hierarchical

Another approach to planning is using Hierarchical Tasks Networks (HTN) to resolve some planning problem. Instead of searching to satisfy a goal, HTNs try to find a decomposition to a root task that fit the initial state requirements and that generate an executable plan.

#### UMCP

One of the first planner to support HTN domains was UCMP by @erol_umcp_1994. It uses Lisp like most of the early planning systems. Apparently PDDL was in part inspired by UCMP's syntax. Like for PDDL, the domain file describes action (called operators here) and their preconditions and effects (called postconditions). The syntax is demonstrated in @lst:ucmp. The interesting part of that language is the way decomposition is handled. Each task is expressed as a set of methods. Each method has an expansion expression that specifies how the plan should be constructed. It also has a pseudo precondition with modal operators on the temporality of the validity of the literals.

``` {#lst:ucmp .lisp caption="Example of the syntax used by UCMP." escapechar=$}
(constants a b c table) ; declare constant symbols
(predicates on clear) ; declare predicate symbols
(compound-tasks move) ; declare compound task symbols
(primitive-tasks unstack dostack restack) ; declare primitive task symbols
(variables x y z) ; declare variable symbols

(operator unstack(x y)
          :pre ((clear x)(on x y))
          :post ((~on x y)(on x table)(clear y)))
(operator dostack (x y)
          :pre ((clear x)(on x table)(clear y))
          :post ((~on x table)(on x y)(~clear y)))
(operator restack (x y z)
          :pre ((clear x)(on x y)(clear z))
          :post ((~on x y)(~clear z)(clear y)(on x z)))

(declare-method move(x y z)
                :expansion ((n restack x y z))
                :formula (and (not (veq y table))
                              (not (veq x table))
                              (not (veq z table))
                              (before (clear x) n)
                              (before (clear z) n)
                              (before (on x y) n)))

(declare-method move(x y z)
                :expansion ((n dostack x z))
                :formula (and (veq y table)
                              (before (clear x) n)
                              (before (on x y) n)))
```

#### SHOP2

The next HTN planner is SHOP2 by @nau_shop2_2003. It remains to this day, one of the reference implenentation of an HTN planner. The SHOP2 formalism is quite similar to UCMP's: each method has a signature, a precondition formula and eventually a decomposition description. This decomposition is a set of methods like in UCMP. The methods can be also partially ordered allowing more expressive plans. An example of the syntax of a method is given in @lst:shop2.

```{#lst:shop2 .lisp caption="Example of method in the SHOP2 language." escapechar=$}
(:method
  ; head
    (transport-person ?p ?c2)
  ; precondition
    (and
      (at ?p ?c1)
      (aircraft ?a)
      (at ?a ?c3)
      (different ?c1 ?c3))
  ; subtasks
    (:ordered
      (move-aircraft ?a ?c1)
      (board ?p ?a ?c1)
      (move-aircraft ?a ?c2)
      (debark ?p ?a ?c2)))
```

#### HDDL

A more recent example of HTN formalism comes from the PANDA framework by @bercher_hybrid_2014. This framework is considered the current standard of HTN planning and allows for great flexibility in domain description. PANDA takes previous formalism and generalize them into a new language exposed in @lst:hddl. That language was called HDDL after its most used file extention.

```{#lst:hddl .lisp caption="Example of HDDL syntax as used in the PANDA framework." escapechar=$}
(define (domain transport)
  (:requirements :typing :action-costs)
  (:types
        location target locatable - object
        vehicle package - locatable
        capacity-number - object
  )
  (:predicates 
     (road ?l1 ?l2 - location)
     (at ?x - locatable ?v - location)
     (in ?x - package ?v - vehicle)
     (capacity ?v - vehicle ?s1 - capacity-number)
     (capacity-predecessor ?s1 ?s2 - capacity-number)
  )

  (:task deliver :parameters (?p - package ?l - location))
  (:task unload :parameters (?v - vehicle ?l - location ?p - package))

  (:method m-deliver
    :parameters (?p - package ?l1 ?l2 - location ?v - vehicle)
    :task (deliver ?p ?l2)
     :ordered-subtasks (and
      (get-to ?v ?l1)
      (load ?v ?l1 ?p)
      (get-to ?v ?l2)
      (unload ?v ?l2 ?p))
  )
  (:method m-unload
    :parameters (?v - vehicle ?l - location ?p - package ?s1 ?s2 - capacity-number)
    :task (unload ?v ?l ?p)
    :subtasks (drop ?v ?l ?p ?s1 ?s2)
  )

  (:action drop
    :parameters (?v - vehicle ?l - location ?p - package ?s1 ?s2 - capacity-number)
    :precondition (and
        (at ?v ?l)
        (in ?p ?v)
        (capacity-predecessor ?s1 ?s2)
        (capacity ?v ?s1)
      )
    :effect (and
        (not (in ?p ?v))
        (at ?p ?l)
        (capacity ?v ?s2)
        (not (capacity ?v ?s1))
      )
  )
)
```

#### HPDDL

A very recent language proposition was done by @ramoul_mixedinitiative_2018. He proposes HPDDL with a simple syntax similar to the one of UCMP. In @lst:hpddl we give an example of HPDDL method. Its expressive power seems similar to that of UCMP and SHOP. Except for a possible commercial integration with PDDL4J [@pellier_pddl4j_2017], there doesn't seem to have any advantages compared to earlier works.

``` {#lst:hpddl .lisp caption="Example of HPDDL syntax as described by Ramoul." escapechar=$}
(:method do_navigate
  :parameters(?x - rover ?from ?to - waypoint)
  :expansion((tag t1 (navigate ?x ?from ?mid))
             (tag t2 (visit ?mid))
             (tag t3 (do_navigate ?x ?mid ?to))
             (tag t4 (unvisited ?mid)))
  :constraints((before (and (not (can_traverse ?x ?from ?to)) (not (visited ?mid))
                            (can_traverse ?x ?from ?mid)) t1)))
```

### Ontological

Another old idea was to merge automated planning and other artificial intelligence fields with knowledge representation and more specifically ontologies. Indeed, since the "semantic web" is already widespread for service description, why not make planning compatible with it to ease service composition ?

#### WebPDDL

This question finds it first answer in 2002 with WebPDDL. This language, explicited in @lst:webpddl, is meant to be compatible with RDF by using URI identifiers for domains [@mcdermott_representing_2002]. The syntax is inspired by PDDL, but axioms are added as constraints on the knowledge domain. Actions also have a return value and can have variables that aren't dependant on their parameters. This allows for greater expressivity than regular PDDL, but can be partially emulated using PDDL 3.1 constraints and object fluents.

```{#lst:webpddl .lisp caption="Example of WebPDDL syntax by Mc Dermott." escapechar=$}
(define (domain www-agents)
  (:extends (uri "http://www.yale.edu/domains/knowing")
            (uri "http://www.yale.edu/domains/regression-planning")
            (uri "http://www.yale.edu/domains/commerce"))
  (:requirements :existential-preconditions :conditional-effects)
  (:types Message - Obj Message-id - String)
  (:functions  (price-quote ?m - Money)
               (query-in-stock ?pid - Product-id)
               (reply-in-stock ?b - Boolean) - Message)
  (:predicates (web-agent ?x - Agent)
               (reply-pending a - Agent id - Message-id msg - Message)
               (message-exchange ?interlocutor - Agent
                                 ?sent ?received - Message
                                 ?eff - Prop)
               (expected-reply a - Agent sent expect-back - Message))
  (:axiom
      :vars (?agt - Agent ?msg-id - Message-id ?sent ?reply - Message)
      :implies (normal-step-value (receive ?agt ?msg-id) ?reply)
      :context (and (web-agent ?agt)
                    (reply-pending ?agt ?msg-id ?sent)
                    (expected-reply ?agt ?sent ?reply)))
  (:action send
      :parameters (?agt - Agent ?sent - Message)
      :value (?sid - Message-id)
      :precondition (web-agent ?agt)
      :effect (reply-pending ?agt ?sid ?sent))
  (:action receive
    :parameters (?agt - Agent ?sid - Message-id)
    :vars (?sent - Message ?eff - Prop)
    :precondition (and (web-agent ?agt) (reply-pending ?agt ?sid ?sent))
    :value (?received - Message)
    :effect (when (message-exchange ?agt ?sent ?received ?eff) ?eff)))
```

#### OPT

This previous work was updated by @mcdermott_opt_2005. The new version is called OPT and allows for some further expressivity. It can express hierarchical domains with links between actions and even advanced data structure. The syntax is mostly an update of WebPDDL. In @lst:opt, we can see that the URI were replaced by simpler names, the action notation was simplified to make the parameter and return value more natural. Axioms were replaced by facts with a different notation.

```{#lst:opt .lisp caption="Example of the updated OPT syntax as described by Mc Dermott." escapechar=$}
(define (domain www-agents)
  (:extends knowing regression-planning commerce)
  (:requirements :existential-preconditions :conditional-effects)
  (:types Message - Obj Message-id - String )
  (:type-fun (Key t) (Feature-type (keytype t)))
  (:type-fun (Key-pair t) (Tup (Key t) t))
  (:functions (price-quote ?m - Money)
              (query-in-stock ?pid - Product-id)
              (reply-in-stock ?b - Boolean) - Message)
  (:predicates (web-agent ?x - Agent)
               (reply-pending a - Agent id - Message-id msg - Message)
               (message-exchange ?interlocutor - Agent
                                 ?sent ?received - Message
                                 ?eff - Prop)
               (expected-reply a - Agent sent expect-back - Message))
  (:facts
    (freevars (?agt - Agent ?msg-id - Message-id
               ?sent ?reply - Message)
      (<- (and (web-agent ?agt)
               (reply-pending ?agt ?msg-id ?sent)
               (expected-reply ?agt ?sent ?reply))
               (normal-value (receive ?agt ?msg-id) ?reply))))
  (:action (send ?agt - Agent ?sent - Message) - (?sid - Message-id)
    :precondition (web-agent ?agt)
    :effect (reply-pending ?agt ?sid ?sent))
  (:action (receive ?agt - Agent ?sid - Message-id) - (?received - Message)
    :vars (?sent - Message ?eff - Prop)
    :precondition (and (web-agent ?agt)
                       (reply-pending ?agt ?sid ?sent))
    :effect (when (message-exchange ?agt ?sent ?received ?eff) ?eff)))
```


---

\newpage

To illustrate the concrete use of SELF, we present an application to the domain of automated planning. In that field, the general consensius gravitated toward using the Planning Domain Definition Language (PDDL) as a standard to express planning domains. However, the comunity is very diverse and each planner use its own internal representation which created the needs for a dozen of specialized extensions to PDDL, as well as several revision and a zoo of unofficial derivated languages.

In some cases this standardization is almost impossible. As an example, Hierarchical Task Networks (HTN) is a planning technique that doesn't fit classical domain description in its standard form. HTN usually aren't goal driven and cannot insert actions (called tasks) arbitrarilly into plans but must conform to the task network structure through chosen decomposition operations. Any attempts at integrating HTN into PDDL in its entirety must rewrite completely the problem description as well as allow to encode plans into the domain description with all the possible orders and constraint paradigm used in practice.

Most planner engine need an external software or library to translate PDDL into a useable form. This means that planners are reasoning on a totally different language that has been preprocessed to remove anything that is deemed not useful for the direct resolution of that particular problem. While it is a good approach when optimizing for raw performance, it is catastrophic for any other criterias such as expressivity, and the flexibility of the plan and planning process. For example, such methods are so bad at repairing a plan that it is often better to replan from scratch. It is also impossible to take advantage of the semantic informations contained in the original description of the problem once this data is literally lost in translation.



Those issues are what initially drove us to create SELF as a way to express domains, plans and any associated knowledge.

Implementations

Other frameworks

## Planning paradigms

### Classical

### Probabilistic

### Plan space

### Case based

### Hierarchical

Other ?



## General Planning Framework {#sec:framework}

First we need to formally define a planning framework that is as generalized as possible.

::: {.definition name="Fluent"} :::
A planning fluent $f$ is a parameterized entity $r(arg_1, arg_2, …, arg_n)$ where:

* $r \in R$ is a relation/function.
* $arg_{i \in [1,n]} \in \mathcal{E}$ are the arguments (possibly quantified).
* $n = |r|$ is the arity of $r$.

Fluents are signed. Negative fluents are noted $\neg f$ and behave as a logical complement. The quantifiers are affected by the sign of the fluents. We do not use the closed SELF hypothesis: fluents are only satisfied when another compatible fluent is provided. Sets of fluents have a boolean value that equals the conjunction of all its fluents.
:::::::::::::::::::::::::::::::::::

If $|r| = 2$ a fluent using $r$ can be expressed as a statement. Otherwise, it is represented as a parameterized entity. We use the exclusive quantifier for negation. For simplicity we will use list as conjonction formula (since it is the most used relation in planning).

::: {.definition #def:action name="Action"} :::
An action is a parametrized tuple $a(args)=\langle \nu, pre, \mathit{eff}, ¢, d, prob, methods \rangle$ where:

* $\nu$ holds the **name** of the action.
* $\mathit{pre}$ and $\mathit{eff}$ are sets of fluents that are respectively the **preconditions and the effects** of the action.
* $¢$ is the intrisic **cost** of the action.
* $d$ is the intrinsic **duration** of the action.
* $prob$ is the prior **probability** of the action succeeding.
* $methods$ is a set of **methods** that decompose the action into smaller simpler ones.
::::::::::::::::::::::::::::::::::::::::::::::::::

Operators take many names in difference planning paradigm : actions, steps, tasks, etc. In our case we call operators, all fully lifted actions and actions are all the instances possibles (including operators).

::: {.definition name="Partial Plan / Method"} :::
A partially ordered plan is an *acyclic* directed graph $\pi = (A_\pi, L)$, with:

* $A_\pi$ the set of **steps** of the plan as vertices. A step is an action belonging in the plan. $A_\pi$ must contain an initial step $I_\pi$ and goal step $G_\pi$ as convinience for certain planning paradigms.
* $L$ the set of **causal links** of the plan as edges.
We note $l = a_s \xrightarrow{c} a_t$ the link between its source $a_s$ and its target $a_t$ caused by the set of fluents $c$. If $c = \emptyset$ then the link is used as an ordering constraint.
:::::::::::::::::::::::::::::::::

This definition can express any kind of plans, either temporal, fully or partially ordered or even lose hierarchical plans (using the methods of the actions).

::: {.definition name="Problem"} :::
The planning problem is defined as the **root operator** $a_0$ which methods are potential solutions of the problem. Its preconditions and effects are respectively used as initial state and goal description.
::::::::::::::::::::::::::::::::::::

This problem description allows for all planning paradigm known to the authors.

### Action type

(Définition)

### Plan type

(Définition)

### Problem type

(Définition)


## Using SELF for planning

In order to describe this planning framework into SELF, we simply put all fields of the actions into properties. Entities are used as fluents, and the entire knwoledge domain as constraints. We use parameterized types as specified @before.

```{#lst:planning .java caption="Content of the file \"planning.w\""}
"lang.w" = ? ; //include vanilla SELF
(pre,eff)::Property(Action,Group);
(¢,d,prob) ::Property(Action,Float);
methods ::Property(Action,Plan);
Plan = Group(Statement);
-> ::Property(Action,Action);
Fluent = Entity;
```

### Equivalence

A first example of how SELF can be used to replace PDDL takes place using the well known 'block SELF' planning domain from the IPCs.



And here is its equivalent in SELF :

```{#lst:block_SELF .java caption="BlockSELF in SELF"}
"planning.w" = ? ; //include base terminology

(! on !, held(!), down(_)) :: Fluent;

pickUp(x) pre (~ on x, down(x), held(~));
pickUp(x) eff (~(down(x)), held(~));

putDown(x) pre (held(x));
putDown(x) eff (held(~), down(x));

stack(x, y) pre (held(x), ~ on y);
stack(x, y) eff (held(~), x on y);

unstack(x, y) pre (held(~), x on y);
unstack(x, y) eff (held(x), ~ on y);
```

The functions in PDDL are replaced by parameterized entities with a unicity constraint on the parameter (only one instance can be true at once). If the value of the function is needed one can simply use the solution quantifier to retrieve the value. 

In this example, the `no-block` constant is replaced by the exclusive quantifier. Since the domain has one type, it isn't required to define it as all objects of the domains are compatible with all fluents. The predicate `on-table` is replaced with the unary predicate `down`. The function `in-hand` is replaced with the unary exclusive predicate `held`. For the unary function `on-block` we use a binary property `on` used in a statement.



### Differences with PDDL

SELF is more consice than PDDL. It will infere most types and declaration. Variables are also infered if they are used more than once in a statement and also part of parameters.

While PDDL uses a fixed set of extensions to specify the capabilities of the domain, SELF uses inclusion of other files to allow for greater flexibility. In PDDL, everything must be declared while in SELF, type inference allows for usage without definition. It is interesting to note that the use of variables names `x` and `y` are arbitrary and can be changed for each statement and the domain will still be functionally the same. The line 3 in @lst:block_pddl is a specific feature of SELF that is absent in PDDL. It is possible to specify constraints on the cardinality of properties. This limits the number of different combination of values that can be true at once. This is typically done in PDDL using several predicate or constraints.

Most of the differences can be sumarized saying that 'SELF do it once, PDDL needs it twice'. This doesn't only mean that SELF is more compact but also that the expressivity allows for a drastic reduction of the search space if taken into account. @thiebaux_defense_2005 advocate for the recognition of the fact that expressivity isn't just a convinience but is crucial for some problem and that treating it like an obstacle by trying to compile it away only makes the problem worse. If a planner is agnostic to the domain and problem, it cannot take advantages of clues that the instanciation of an action or even its name can hold [@babli_use_2015].

Whatever the time and work that an expert spend on a planning domain it will always be incomplete and fixed. SELF allows for dynamical extension and even adresses the use of reified actions as parameters. Such a framework can be useful in multi-agent systems where agents can commnicate composite actions to instruct another agent. It can also be useful for macro-action learning that allows to learn hierarchical domains from repeating observations. It can also be used in online planning to repair a plan that failed. And at last this mechanism can be used for explanation or inference by making easy to map two similar domains together. (lots of @citation).

Also another difference between SELF and PDDL is the underlying planning framework. We presented the one of SELF but PDDL seems to suppose a more classical state based formalism. For example the fluents are of two kind depending if they are used as precondition or effects. In the first case, the fluent is a formula that is evaluated like a predicate to know if the action can be executed in any given state. Effects are formula enforcing the values of existing fluent in the state. SELF just suppose that the new knowledge is enforcing and that the fluents are of the same kind since verification about the coherence of the actions are made prior to its application in planning.
